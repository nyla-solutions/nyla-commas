apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'findbugs'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
version = '0.1'
group = "com.github.nyla-solutions"
archivesBaseName = "nyla.solutions.commas"
	
jar {
    manifest {
        attributes 'Implementation-Title': 'Nyla Commas',
                   'Implementation-Version': version
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compile
	}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}



repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
	compile group: 'com.github.nyla-solutions', name: 'nyla.solutions.core', version: '1.0.0'
	compile group: 'com.github.nyla-solutions', name: 'nyla.solutions.json', version: '0.0.1'
	compile group:  'org.aspectj', name: 'aspectjrt', version: '1.8.4'		
	compile group: 'org.reflections', name: 'reflections', version: '0.9.5'
	// https://mvnrepository.com/artifact/junit/junit
	testCompile group: 'junit', name: 'junit', version: '4.12'
    
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
	  repositories {
		  mavenDeployer {
		      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

		      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
		        authentication(userName: ossrhUsername, password: ossrhPassword)
		      }


	      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	        authentication(userName: ossrhUsername, password: ossrhPassword)
	      }

	      pom.project {
	        name 'Nyla Commas'
	        packaging 'jar'
	        // optionally artifactId can be defined here 
	        description 'Command pattern implementation for building services.'
	        url 'https://github.com/nyla-solutions/nyla/tree/master/nyla.solutions.commas'
	        
	        scm {
	            url 'https://github.com/nyla-solutions/nyla.git'
	          }
	        
	        
	        licenses {
	          license {
	            name 'The Apache License, Version 2.0'
	            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	          }
	        }

	        developers {
	          developer {
	            id 'ggreen'
	            name 'Gregory Green'
	            email 'ggreen@g-solution.net'
	          }
	        }
	      }
	    }
	  }
}
